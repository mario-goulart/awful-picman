(define (render-tag tag)
  `(div (@ (class "tag-container"))
        (a (@ (href ,(sprintf "~a/?include-tags=~a"
                              (make-pathname (filters-web-dir) "by-tags")
                              tag)))
           ,tag)
        (literal "&nbsp;")
        (span (@ (class "edit-tag glyphicon glyphicon-edit")))))

(define (render-tag-edit-modal)
  `(div (@ (class "modal fade")
           (id "tag-edit-modal"))
        (div (@ (class "modal-dialog"))
             (div (@ (class "modal-content"))
                  (div (@ (class "modal-header"))
                       (button (@ (type "button")
                                  (class "close")
                                  (data-dismiss "modal")
                                  (aria-label "Close"))
                               (span (@ (aria-hidden "true"))
                                     (literal "&times;")))
                       (h4 (@ (class "modal-title"))
                           ,(_ "Edit tag")))
                  (div (@ (class "modal-body"))
                       (input (@ (type "text")
                                 (id "tag-new-text")
                                 (value "")))
                       (hr)
                       (p ,(_ "Remove tag?")
                          (literal "&nbsp;")
                          (input (@ (type "checkbox")
                                    (id "tag-remove")))))
                  (div (@ (class "modal-footer"))
                       (span (@ (id "tag-old-text") ;; a hack: holds the old tag
                                (style "visibility: hidden; display: none"))
                             "")
                       (button (@ (type "button")
                                  (data-dismiss "modal")
                                  (class "btn btn-default"))
                               ,(_ "Cancel"))
                       (button (@ (id "save-tag")
                                  (type "button")
                                  (class "btn btn-primary"))
                               ,(_ "Submit")))))))


(define (render-tags)
  `(div (@ (id "content")) ;; FIXME: move to a more generic place
        ,(render-tag-edit-modal)
        (div (@ (id "tags-list"))
             ,(let ((tags (db-tags)))
                (if (null? tags)
                    `(div (@ (id "no-tag"))
                          ,(_ "No tag has been created yet."))
                    (map render-tag tags))))))
