(define (tag-include tags)
  (render-dynamic-inputs 'tag_include 0 tags name: "include-tags"))

(define (tag-exclude tags)
  (render-dynamic-inputs 'tag_exclude 1 tags name: "exclude-tags"))

(define (render-filter-input include-tags exclude-tags)
  (create-dynamic-input-ajax 'tag_include "/db/tags" name: "include-tags")
  (create-dynamic-input-ajax 'tag_exclude "/db/tags" name: "exclude-tags")
  (add-dynamic-input-javascript-utils)

  `(,(render-top-bar 'filter)
    (div (@ (class "filter-input"))
         (form (@ (method "get")
                  (action ,(filters-web-dir)))
               (div (@ (class "row"))
                    (div (@ (class "span3"))
                         ,(_ "Show pictures tagged with "))
                    (div (@ (class "span3"))
                         ,(tag-include include-tags)
                         ","))
               (div (@ (class "row"))
                    (div (@ (class "span3"))
                         ,(_ " except those tagged with "))
                    (div (@ (class "span3"))
                         ,(tag-exclude exclude-tags)))
               (input (@ (type "submit")
                         (value ,(_ "Filter"))))))))

(define (form-tags include-tags exclude-tags)
  (append (map (lambda (tag)
                 (cons 'include-tags tag))
               include-tags)
          (map (lambda (tag)
                 (cons 'exclude-tags tag))
               exclude-tags)))

(define (render-filtered-pics include-tags exclude-tags page-num)
  `(,(render-filter-input include-tags exclude-tags)
    ,(if (null? include-tags)
         '()
         (let* ((filtered-pic-paths (db-tag-filter include-tags exclude-tags))
                (matches (length filtered-pic-paths)))
           (debug 2 "render-filtered-pictures: filter results: ~S" filtered-pic-paths)
           `((div (@ (id "filter-matches"))
                  (h4 ,(if (zero? matches)
                           (_ "No match")
                           `(,matches " " ,(if (= 1 matches)
                                               (_ "match")
                                               (_ "matches"))))))
             ,(render-paginated-pics filtered-pic-paths
                                     page-num
                                     'filter
                                     url-vars/vals: (form-tags include-tags exclude-tags)))))))
